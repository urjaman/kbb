# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Hacker: Urja ;P

pkgbase=linux-apu
pkgver=5.10.228
pkgrel=1
pkgdesc='LTS Linux (with APU spice)'
url="https://www.kernel.org/"
arch=(x86_64)
license=(GPL2)
makedepends=(
  bc kmod libelf cpio perl tar xz xmlto git
)
options=('!strip')
_srcname=linux-5.10
source=(
	"https://www.kernel.org/pub/linux/kernel/v5.x/${_srcname}.tar.xz"
        "https://www.kernel.org/pub/linux/kernel/v5.x/patch-${pkgver}.xz"
	'apu.patch'
	config         # the main kernel config file
  90-linux.hook # hook to update the in-flash kernel (on coreboot if needed...)
)

prepare() {
  cd $_srcname

  # hack to allow git apply to work on non-git files while in a git directory
  export GIT_DIR=/var/empty

  # add upstream patch
  git apply --whitespace=nowarn ../patch-${pkgver}

  # APU patchset
  git apply ../apu.patch

  unset GIT_DIR

  # Append (if -rc*) or set ${pkgrel} as extraversion
  sed -ri "s/^(EXTRAVERSION =) ?(-rc[^-]*|).*/\1 \2-${pkgrel}/" Makefile

  echo "Setting config..."
  cat "${srcdir}/config" > ./.config
  make olddefconfig
  cp ./.config ../../config

  make -s kernelrelease > version
  echo "Prepared $pkgbase version $(<version)"
}

build() {
  cd $_srcname
  make -j2 all
}

_package() {
  pkgdesc="The $pkgdesc kernel and modules"
  depends=(coreutils kmod initramfs)
  optdepends=('crda: to set the correct wireless channels of your country'
              'linux-firmware: firmware images needed for some devices')
  provides=(VIRTUALBOX-GUEST-MODULES WIREGUARD-MODULE)
  replaces=(wireguard-lts)

  cd $_srcname
  local kernver="$(<version)"
  local modulesdir="$pkgdir/usr/lib/modules/$kernver"

  echo "Installing boot image..."
  # systemd expects to find the kernel here to allow hibernation
  # https://github.com/systemd/systemd/commit/edda44605f06a41fb86b7ab8128dcf99161d2344
  install -Dm644 "$(make -s image_name)" "$modulesdir/vmlinuz"

  # Used by mkinitcpio to name the kernel
  echo "$pkgbase" | install -Dm644 /dev/stdin "$modulesdir/pkgbase"

  echo "Installing modules..."
  make INSTALL_MOD_PATH="$pkgdir/usr" INSTALL_MOD_STRIP=1 modules_install

  # remove build and source links
  rm "$modulesdir"/{source,build}

  # sed expression for following substitutions
  local _subst="
    s|%PKGBASE%|${pkgbase}|g
    s|%KERNVER%|${_kernver}|g
    s|%EXTRAMODULES%|${_extramodules}|g
  "
  # Add the hoook...
  sed "${_subst}" ../90-linux.hook |
    install -Dm644 /dev/stdin "${pkgdir}/usr/share/libalpm/hooks/90-${pkgbase}.hook"
}

pkgname=("$pkgbase")
for _p in "${pkgname[@]}"; do
  eval "package_$_p() {
    $(declare -f "_package${_p#$pkgbase}")
    _package${_p#$pkgbase}
  }"
done

# vim:set ts=8 sts=2 sw=2 et:

sha256sums=('dcdf99e43e98330d925016985bfbc7b83c66d367b714b2de0cbbfcbf83d8ca43'
            'e79744011effb3dd0b69b9e7166adaa4a7ba8232a820b6d4868a42eebb92e117'
            '227eee8e2ec82dae284d0e55777afa46cedc18d433deb179a05a70f84ce09a3a'
            '2a298bd6da478aee9e972c01a36e15c501008ab02cf7f817db1c28e6808dfe13'
            '082385d0802ab8290564df1f0973ce82cc328bc438bf91abfa5a8df40401e417')
